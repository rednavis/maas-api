plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.2.4.RELEASE' apply false
    id 'com.google.cloud.tools.jib' version '2.0.0' apply false
}

ext {
    maasApiVersion = '0.0.1-SNAPSHOT'
    lombokVersion = '1.18.12'
    mapstructVersion = '1.3.1.Final'
    springBootVersion = '2.2.4.RELEASE'
    jjwtVersion = '0.11.0'
    swaggerVersion = '3.0.0-SNAPSHOT'
    h2Version = '1.4.200'
    jupiterVersion = '5.6.0'
}

allprojects {
    apply plugin: 'project-report'
    // needed because of multi-module configuration
    apply plugin: 'jacoco'
    apply plugin: 'io.spring.dependency-management'

    repositories {
        jcenter()
        mavenCentral()
        maven { url 'http://oss.jfrog.org/artifactory/oss-snapshot-local/' }
    }

    dependencyManagement {
        dependencies {
            //Server
            dependency "org.projectlombok:lombok:${lombokVersion}"
            dependency "org.mapstruct:mapstruct:${mapstructVersion}"
            dependency "org.mapstruct:mapstruct-processor:${mapstructVersion}"

            dependency "org.springframework.boot:spring-boot-starter-security:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-data-mongodb-reactive:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-starter-webflux:${springBootVersion}"
            dependency "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"

            dependency "io.jsonwebtoken:jjwt-impl:${jjwtVersion}"
            dependency "io.jsonwebtoken:jjwt-jackson:${jjwtVersion}"

            dependency "io.springfox:springfox-swagger2:${swaggerVersion}"
            dependency "io.springfox:springfox-swagger-ui:${swaggerVersion}"
            dependency "io.springfox:springfox-spring-webflux:${swaggerVersion}"

            dependency "com.h2database:h2:${h2Version}"

            //Test
            dependency "org.junit.jupiter:junit-jupiter:${jupiterVersion}"
            dependency "org.springframework.boot:spring-boot-starter-test:${springBootVersion}"
        }
    }
}

// assumed that all subprojects are java-based modules
subprojects {
    apply from: "${rootDir}/gradle/java.gradle"
    apply from: "${rootDir}/gradle/checkstyle.gradle"

    dependencies {
        implementation 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        implementation 'org.mapstruct:mapstruct'
        annotationProcessor 'org.mapstruct:mapstruct-processor'

        annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

        testImplementation 'org.junit.jupiter:junit-jupiter'
        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'junit', module: 'junit'
        }
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            html.enabled false
        }
    }
}

task codeCoverageReport(type: JacocoReport) {
    executionData fileTree(project.rootDir.absolutePath).include("**/build/jacoco/*.exec")

    subprojects.each {
        sourceSets it.sourceSets.main
    }

    reports {
        xml.enabled true
        xml.destination file("${buildDir}/reports/jacoco/report.xml")
        html.enabled false
        csv.enabled false
    }
}

// https://github.com/codecov/example-gradle#-do-you-support-multi-module-projects
codeCoverageReport.dependsOn {
    subprojects*.test
}

wrapper {
    gradleVersion = "6.2"
}
